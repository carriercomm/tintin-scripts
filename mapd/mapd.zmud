#CLASS {mapd} {enable}
#ALIAS findme {
  #var namel {%mapquery( NAME = '@name')}
  #var descl {}
  #var exitl {}
  #if (%numitems( @namel) = 0) {#echo no matching room names} {
    #if (%numitems( @namel) = 1) {
      #echo room found by name
      #teleport @namel
      } {
      #forall @namel {#if (%pos( @desc, %roomdesc( %i))) {#additem descl %i}}
      #if (%numitems( @descl) = 0) {#echo no matching room descriptions} {
        #if (%numitems( @descl) = 1) {
          #echo room found by description
          #teleport @descl
          } {
          #var exitsf {%sort( %lower( %replace( @exits, " ", "|")))}
          #forall @descl {#if (@exitsf =~ "%sort(%roomexit( %i))") {#additem exitl %i}}
          #if (%numitems( @exitl) = 0) {
            #echo no matching room exits, possible hidden door confusion
            #echo unable to find current room
            } {
            #if (%numitems( @exitl) = 1) {
              #echo room found by exits
              #teleport @exitl
              } {
              #if (%ismember( %roomkey, @exitl)) {#echo multiple rooms found by exits, current room is in the list, no action taken} {
                #echo multiple rooms found by exits, choosing the first match
                #teleport %item( @exitl, 1)
                }
              }
            }
          }
        }
      }
    }
  #unvar namel
  #unvar descl
  #unvar exitl
  }
#ALIAS flee {
  #var foundroom {0}
  #if (%numitems( %roomexit( @num)) = 1) {#var foundroom {%roomlink( @num, %roomexit( @num))}} {#FORALL %roomexit( @num) {#FORALL %roomlink( @num, %i) {#if (@name =~ "%roomname( %j)") {#if (%pos( @desc, %roomdesc( %j))) {#if (%sort( %lower( %replace( @exits, " ", "|"))) =~ "%sort(%roomexit( %j))") {#var foundroom %j}}}}}}
  #if (@foundroom) {
    #echo found location
    #teleport @foundroom
    } {
    #echo unable to find location by exits, performing in depth search
    findme
    }
  
  }
#VAR desc {} {}
#VAR name {} {}
#VAR exits {} {}
#VAR num {} {}
#VAR findme {0} {0}
#VAR flee {0} {0}
#VAR foundroom {} {}
#TRIGGER {^ACTION: flee} {
  #t+ desc
  #var num {%roomnum( )}
  #var findme {0}
  #var flee {1}
  }
#TRIGGER {^ACTION: findme} {
  #t+ desc
  #var findme {1}
  #var flee {0}
  }
#TRIGGER {^ACTION: move %1} {
  #move %1
  #t+ desc
  #var num {%roomnum( )}
  #var findme {0}
  #var flee {0}
  }
#TRIGGER {^ACTION: look} {
  #t+ desc
  #var findme {0}
  #var flee {0}
  }
#TRIGGER {^ROOM: %1} {#var name {%1}}
#TRIGGER "desc" {^DESC: %1} {
  #var desc {%1}
  #t- desc
  } "" {disable}
#TRIGGER {^EXITS: %1} {
  #var exits {%1}
  #if (@findme = 1) {
    #var findme {0}
    findme
    }
  #if (@flee = 1) {
    #var flee {0}
    flee
    }
  }
#CLASS 0

